include("include/SOVIETInstructions.txt");
include("settings.txt");

defineVariable(int, population);
defineVariable(int, n);
defineVariable(int, i);
defineVariable(int, j);
defineVariable(Person, person);
defineVariable(Building, building);
defineVariable(int, rnd);
defineVariable(int, day);
defineVariable(int, year);

defineVariable(int, max_sick);
defineVariable(int, rnd_sick_no);
defineVariable(int, rnd_sick);

defineFunction(Clamp, float, float:x, float:y, float:z)
{
    if (x < y)
	{
        x = y;
    }
	elseif(x > z) {
		x = z;
	}
	return(x);
}

defineFunction(main, void)
{
	InitConstants();
	InitSettings();
	//check correctness of the constants in the settings file
	SCHOOL_AGE = Clamp(SCHOOL_AGE, 0.0, 20.5);
	UNIVERSITY_AGE = Clamp(UNIVERSITY_AGE, 0.0, 20.5);
	SEASONAL_DISEASES_STRENGTH = Clamp(SEASONAL_DISEASES_STRENGTH, 0.0, 1.0);
	while(1 ? 1)
	{
		Script_EnterCriticalSection();
		Building_GetNumberOfBuildings(n);
		population = 0;
		for(i=0, i < n, i=i+1)
		{
			building.GetDataByIndex(i);
			if(building.nType ? BUILDINGTYPE_LIVING)
			{
				population = population + building.nWorkersNum;
			}
		}
		for(i=0, i < population, i=i+1)
		{
			//children have to go to school until they are SCHOOL_AGE years old
			person.GetDataByIndex(i);
			if(person.fAge < SCHOOL_AGE & person.fEducation > 0.9 & (person.fEducation < 1.0 | INSTANT_EFFECT ? 1))
			{
				Person_SetEducation(i, 0.9);
			}
			//university students can't graduate until they are UNIVERSITY_AGE years old
			if(person.fAge < UNIVERSITY_AGE & person.fEducation > 1.9 & (person.fEducation < 2.0 | INSTANT_EFFECT ? 1))
			{
				Person_SetEducation(i, 1.9);
			}
			//seriously ill patients stay at hospital longer
			if(person.fStatusHealth < 0.2 & HOSPITAL_PROB_DENOMINATOR > 90)
			{
				Random(rnd);
				rnd = rnd % HOSPITAL_PROB_DENOMINATOR;
				if(rnd < 90 & person.fStatusHealth > 0.15)
				{
					Person_SetStatus(i, PERSON_STATUS_HEALTH, 0.15);
				}
			}
		}
		//seasonal diseases
		Date_GetCurrentDate_D365Y(day, year);
		//the additional probability of getting ill starts increasing in October, reaches maximum in January and then decreases until April
		max_sick = 0;
		if(day > 270)
		{
			max_sick = (day-270) * population * SEASONAL_DISEASES_STRENGTH / 95.0;
		}
		elseif (day < 95)
		{
			max_sick = (95-day) * population * SEASONAL_DISEASES_STRENGTH / 95.0;
		}
		if(max_sick > 0)
		{
			Random(rnd_sick_no);
			rnd_sick_no = rnd_sick_no % max_sick;
			for(j=0, j<rnd_sick_no, j=j+1)
			{
				Random(rnd_sick);
				rnd_sick = rnd_sick % population;
				person.GetDataByIndex(rnd_sick);
				if(person.fStatusHealth > 0.45)
				{
					Person_SetStatus(rnd_sick, PERSON_STATUS_HEALTH, 0.45);
				}
			}
		}
		Script_LeaveCriticalSection();
		Script_Sleep(9.5);
	}
	end();
}
