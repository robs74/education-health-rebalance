include("include/SOVIETInstructions.txt");
include("settings.txt");

defineVariable(int, population);
defineVariable(int, n);
defineVariable(int, i);
defineVariable(int, j);
defineVariable(int, k);
defineVariable(Person, person);
defineVariable(Building, building);
defineVariable(int, rnd);
defineVariable(int, day);
defineVariable(int, year);

defineVariable(int, max_sick);
defineVariable(int, rnd_sick_no);
defineVariable(int, rnd_sick);
defineVariable(int, population_rnd);
defineVariable(int, rnd_sick2);

defineFunction(Clamp, float, float:x, float:y, float:z)
{
    if (x < y)
	{
        x = y;
    }
	elseif(x > z) {
		x = z;
	}
	return(x);
}

defineFunction(main, void)
{
	InitConstants();
	InitSettings();
	//check correctness of the constants in the settings file
	SCHOOL_AGE = Clamp(SCHOOL_AGE, 0.0, 20.5);
	UNIVERSITY_AGE = Clamp(UNIVERSITY_AGE, 0.0, 20.5);
	SEASONAL_DISEASES_STRENGTH = Clamp(SEASONAL_DISEASES_STRENGTH, 0.0, 1.0);
	while(1 ? 1)
	{
		Script_EnterCriticalSection();
		Building_GetNumberOfBuildings(n);
		population = 0;
		for(i=0, i < n, i=i+1)
		{
			building.GetDataByIndex(i);
			if(building.nType ? BUILDINGTYPE_LIVING & building.fPercFinished ? 1.0)
			{
				population = population + building.nWorkersNum;
			}
		}
		for(i=0, i < population, i=i+1)
		{
			//children have to go to school until they are SCHOOL_AGE years old
			person.GetDataByIndex(i);
			if(person.fAge < SCHOOL_AGE & person.fEducation > 0.9 & (person.fEducation < 1.0 | INSTANT_EFFECT ? 1))
			{
				Person_SetEducation(i, 0.9);
			}
			//university students can't graduate until they are UNIVERSITY_AGE years old
			if(person.fAge < UNIVERSITY_AGE & person.fEducation > 1.9 & (person.fEducation < 2.0 | INSTANT_EFFECT ? 1))
			{
				Person_SetEducation(i, 1.9);
			}
			//seriously ill patients stay at hospital longer
			if(person.fStatusHealth < 0.2 & HOSPITAL_PROB_DENOMINATOR > 90)
			{
				Random(rnd);
				rnd = rnd % HOSPITAL_PROB_DENOMINATOR;
				if(rnd < 90 & person.fStatusHealth > 0.15)
				{
					Person_SetStatus(i, PERSON_STATUS_HEALTH, 0.15);
				}
			}
		}
		//seasonal diseases
		Date_GetCurrentDate_D365Y(day, year);
		//the additional probability of getting ill starts increasing in October, reaches maximum in January and then decreases until April
		max_sick = 0;
		if(day > 270)
		{
			max_sick = (day-270) * population * SEASONAL_DISEASES_STRENGTH / 95.0;
		}
		elseif (day < 95)
		{
			max_sick = (95-day) * population * SEASONAL_DISEASES_STRENGTH / 95.0;
		}
		if(max_sick > 0)
		{
			Random(rnd_sick_no);
			rnd_sick_no = rnd_sick_no % max_sick;
			for(j=0, j<rnd_sick_no, j=j+1)
			{
				//Random range is 2^15 (=32,768) and it's quite easy to have a population exceeding this number, we therefore divide the population by 2^15, assing the value to population_rnd and sum 2*population_rnd + 1 random integers and finally ensure that this number stays withing the correct range.
				//Notice that if we summed only sum population_rnd random integers, then workers with higher indices would have a smaller chance to be chosen. Indeed, assume that we have a population of 50,000. We sum two random integers resulting in a random integer in the range [0, 65536]. By using the modulo operation, the first 15536 workers have double chances to be chosen. We multiply by 2 and add 1 to make the issue less noticeable (but the distribution still remains nonuniform in general).
				Random(rnd_sick2);
				rnd_sick = rnd_sick2;
				population_rnd = population / 2^15;
				for(k = 0, k < 2 * population_rnd + 1, k = k + 1)
				{
					Random(rnd_sick2);
					rnd_sick = rnd_sick + rnd_sick2;
				}
				rnd_sick = (2 * rnd_sick2) % population; 
				person.GetDataByIndex(rnd_sick);
				if(person.fStatusHealth > SEASONAL_DISEASES_HEALTH)
				{
					Person_SetStatus(rnd_sick, PERSON_STATUS_HEALTH, SEASONAL_DISEASES_HEALTH);
				}
			}
		}
		Script_LeaveCriticalSection();
		Script_Sleep(9.5);
	}
	end();
}
